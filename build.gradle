plugins {
	id 'com.diffplug.blowdryer'
	id 'com.diffplug.spotless-changelog'
}

repositories { mavenCentral() }

group = 干.proj('maven_group', 'the maven group, recommend com.diffplug')
allprojects {
	apply from: 干.file('base/changelog.gradle')
}
apply from: 干.file('spotless/freshmark.gradle')

ext.javadoc_agg='spotless-changelog-lib spotless-changelog-plugin-gradle'
apply from: 干.file('base/sonatype.gradle')

String VER_JUNIT = '4.13.2'
String VER_ASSERTJ = '3.26.0'
String VER_JGIT= '6.10.0.202406032230-r'

subprojects {
	apply from: 干.file('base/java.gradle')
	apply from: 干.file('spotless/java.gradle')

	if (it.name == 'spotless-changelog-lib') {
		ext.maven_name='Spotless Changelog Lib'
		ext.maven_desc='Utilities for parsing and updating a changelog'
		dependencies {
			compileOnly 'pl.tlinkowski.annotation:pl.tlinkowski.annotation.basic:0.2.0'
			testCompileOnly 'pl.tlinkowski.annotation:pl.tlinkowski.annotation.basic:0.2.0'
			implementation 'org.osgi:org.osgi.framework:1.10.0'
			implementation 'com.diffplug.durian:durian-core:1.2.0'
			implementation 'com.diffplug.durian:durian-collect:1.2.0'
			implementation 'com.diffplug.durian:durian-io:1.2.0'
			implementation "org.eclipse.jgit:org.eclipse.jgit:$VER_JGIT"
			implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:$VER_JGIT"
			testImplementation "junit:junit:$VER_JUNIT"
			testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
		}
	} else {
		assert it.name == 'spotless-changelog-plugin-gradle'
		ext.maven_name='Spotless Changelog Gradle Plugin'
		ext.maven_desc='Gradle plugin for parsing and updating a changelog'

		apply from: 干.file('base/gradle-plugin.gradle')
		dependencies {
			implementation project(':spotless-changelog-lib')
			implementation 'com.diffplug.durian:durian-core:1.2.0'
			implementation 'com.diffplug.durian:durian-collect:1.2.0'
			implementation 'com.diffplug.durian:durian-io:1.2.0'
			implementation "org.eclipse.jgit:org.eclipse.jgit:$VER_JGIT"
			testImplementation "junit:junit:$VER_JUNIT"
			testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
			// mock out the time
			implementation 'com.diffplug.durian-globals:durian-globals:1.0.0'
			// TODO: in Gradle 6.5 we could do this with a functionalTest sourceSet to keep it out of publishing
			implementation 'com.diffplug.durian-globals:durian-globals.dev:1.0.0'
		}
		tasks.register('configCacheTest', Test) {
			systemProperty 'com.diffplug.config-cache-test', 'true'
		}
		tasks.named('check') {
			dependsOn 'configCacheTest'
		}
	}
	// it all needs to get published and formatted
	apply from: 干.file('base/maven.gradle')
	apply from: 干.file('base/sonatype.gradle')
}
